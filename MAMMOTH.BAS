'         Mammoth Hunter
10 DIM N$(6, 2), H$(6), P$(6)
20 FOR X = 1 TO 6
30 READ A$: H$(X) = A$
40 READ B$: P$(X) = B$
50 NEXT X
60 S = 12: I = 0
70 FOR X = 1 TO 6
80 FOR Y = 1 TO 2
90 N$(X, Y) = "0": NEXT Y: NEXT X
100 CLS : GOSUB 480
110 PRINT "PICK ONE:"
120 PRINT "1) GO TO SUPPLY CAVE"
130 PRINT "2) GO ON MAMMOTH HUNT"
140 PRINT "3) START OVER"
150 INPUT R$: R = VAL(R$)
160 ON R GOTO 550, 180, 60
170 GOTO 100
180 PRINT CHR$(147)
190 C = INT(RND(1) * 2) + 1
200 IF C = 2 THEN 250
210 PRINT "YOU HAVE SEARCHED ALL DAY."
220 IF N$(6, 2) = "1" THEN 240
230 PRINT "YOU HAVE NO FOOD.  RETURN HOME.": GOTO 740
240 PRINT "YOU EAT YOUR EXTRA FOOD."
250 PRINT : PRINT "YOU FIND A MAMMOTH HERD."
260 IF N$(5, 2) = "1" THEN 300
270 C = INT(RND(1) * 2) + 1
280 IF C = 1 THEN 310
290 PRINT "YOU CAN'T GET CLOSE.  RETURN HOME.": GOTO 740
300 PRINT "YOU HIDE UNDER YOUR MAMMOTH SKIN."
310 IF N$(1, 2) = "1" THEN 330
320 PRINT "YOU HAVE NO SPEAR.  RETURN HOME.": GOTO 740
330 PRINT "YOU KILL A MAMMOTH!"
340 IF N$(4, 2) = "1" THEN 360
350 PRINT "YOU HAVE NO KNIFE.  RETURN HOME.": GOTO 740
360 PRINT "YOU CUT IT UP AND HEAD HOME."
370 C = INT(RND(1) * 3) + 1
380 IF C < 1 THEN 430
390 PRINT "BUT IT STARTS TO SNOW."
400 IF N$(3, 2) = "1" THEN 420
410 PRINT "YOU DROP YOUR CATCH AND RUN HOME.": GOTO 740
420 PRINT "YOU MAKE A FIRE AND STAY WARM."
430 PRINT "YOU ARE A MASTER HUNTER!!"
440 PRINT "PLAY AGAIN? Y/N"
450 INPUT R$
460 IF R$ = "Y" THEN 60
470 END
480 PRINT : PRINT "YOU HAVE:"
490 FOR X = 1 TO 6
500 IF N$(X, 2) = "0" THEN 520
510 PRINT N$(X, 1)
520 NEXT X
530 IF I < 1 THEN PRINT "NOTHING"
540 PRINT : RETURN
550 CLS
560 PRINT "WELCOME TO THE HUNT SUPPLY CAVE."
570 PRINT "YOU HAVE "; S; " SEASHELLS TO TRADE."
580 PRINT : GOSUB 480
590 FOR X = 1 TO 6
600 PRINT X; ") "; H$(X); ": "; P$(X); " SHELLS"
610 NEXT X
620 PRINT " 7 ) LEAVE SUPPLY CAVE"
630 PRINT : PRINT "CHOOSE ITEM"
640 INPUT R$
650 R = VAL(R$): IF R > 7 THEN 550
660 IF R < 1 THEN 550
670 IF R = 7 THEN 750
680 IF VAL(P$(R)) <= S THEN 710
690 PRINT "NOT ENOUGH SHELLS"
700 FOR DE = 1 TO 2500: NEXT DE: GOTO 550
710 I = I + 1: N$(R, 1) = H$(R): S = S - VAL(P$(R))
720 N$(R, 2) = "1"
730 GOTO 550
740 FOR DE = 1 TO 3800: NEXT DE
750 GOTO 100
760 DATA SPEAR, 4,DIGGING STICK, 2
770 DATA FIRE-MAKING STONES, 3,KNIFE, 3
780 DATA MAMMOTH HIDE, 3,EXTRA FOOD, 2





















