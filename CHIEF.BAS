1 CLS
2 PRINT TAB(30); "CHIEF"
4 PRINT TAB(15); "CREATIVE COMPUTING  MORRISTOWN, NEW JERSEY"
6 PRINT : PRINT : PRINT
10 PRINT " I AM CHIEF NUMBERS FREEK, THE GREAT INDIAN MATH GOD."
20 PRINT "ARE YOU READY TO TAKE THE TEST YOU CALLED ME OUT FOR?"
30 INPUT A$
40 IF A$ = "YES" THEN 60
50 PRINT "SHUTUP PALE FACE WITH WISE TOUNGE."
60 PRINT " TAKE A NUMBER AND ADD 3.  DIVIDE THIS NUMBER BY 5 AND"
70 PRINT "MULTIPLY BY 8.  DIVIDE BY 5 AND ADD THE SAME.  SUBTRACT 1."
80 PRINT "  WHAT DO YOU HAVE";
90 INPUT B
100 LET C = (B + 1 - 5) * 5 / 8 * 5 - 3
110 PRINT "I BET YOUR NUMBER WAS "; C; " WAS I RIGHT";
120 INPUT D$
130 IF D$ = "YES" THEN 510
140 PRINT "WHAT WAS YOUR ORIGINAL NUMBER";
150 INPUT K
155 LET F = K + 3
160 LET G = F / 5
170 LET H = G * 8
180 LET I = H / 5 + 5
190 LET J = I - 1
200 PRINT "SO YOU THINK YOU'RE SMART, EH?"
210 PRINT "NOW WATCH."
230 PRINT K; "PLUS 3 EQUALS"; F; ".  THIS DIVIDED BY 5 EQUALS"; G; ";"
240 PRINT "THIS TIMES 8 EQUALS"; H; ".  IF WE DIVIDE BY 5 AND ADD 5,"
250 PRINT "WE GET"; I; ", WHICH, MINUS 1 EQUALS"; J; "."
260 PRINT "NOW DO YOU BELIEVE ME";
270 INPUT Z$
290 IF Z$ = "YES" THEN 510
295 PRINT "YOU HAVE MADE ME MAD!!!"
300 PRINT "THERE MUST BE A GREAT LIGHTNING BOLT!"
305 SLEEP 1
310 PRINT : PRINT
330 FOR X = 30 TO 22 STEP -1
340 PRINT TAB(X); "X X"
350 NEXT X
360 PRINT TAB(21); "X XXX"
370 PRINT TAB(20); "X   X"
380 PRINT TAB(19); "XXX X"
390 FOR Y = 20 TO 13 STEP -1
400 PRINT TAB(Y); "X X"
410 NEXT Y
420 PRINT TAB(12); "XX"
430 PRINT TAB(11); "X"
440 PRINT TAB(10); "*"
450 PRINT : PRINT "#########################": PRINT
452 FOR BEEPER = 1 TO 30
454 BEEP
456 NEXT BEEPER
458 SLEEP 1
470 PRINT "I HOPE YOU BELIEVE ME NOW, FOR YOUR SAKE!!"
480 GOTO 520
510 PRINT "BYE!!!!!"
520 END

